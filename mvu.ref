*$FROM LibraryEx
$EXTERN Apply;

*$FROM refalrawdraw
$EXTERN
  CNFGSetupFullscreen,
  CNFGClearFrame,
  CNFGGetDimensions,
  CNFGSwapBuffers,
  CNFGDrawText,
  CNFGSetBGColor,
  CNFGColor,
  CNFGSetPenX,
  CNFGSetPenY;


$ENTRY RunApplication {
  (e.Title) (e.BgColor) t.InitModel t.Update t.View
    , <Apply t.InitModel> : t.Model
    = <Setup (e.Title) (e.BgColor)>
      <MainLoop t.Model t.Update t.View>
}


Setup {
  (e.Title) (e.BgColor)
    = <CNFGSetupFullscreen (e.Title) 0>
      <CNFGSetBGColor e.BgColor>
}


MainLoop {
  t.Model t.Update t.View
    , <GetMsgs>
    : {
        /* empty */
          , <Apply t.View t.Model> : t.DOM
          = <Render t.DOM>
            <MainLoop t.Model t.Update t.View>;

        e.Msgs
          , <UpdateForEachMsg t.Update t.Model e.Msgs> : t.NewModel
          , <Apply t.View t.Model> : e.DOM
          = <Render e.DOM>
            <MainLoop t.NewModel t.Update t.View>;
      }
}


GetMsgs {
  = /* TODO */
}


UpdateForEachMsg {
  t.Update t.Model /* empty */ = t.Model;

  t.Update t.Model t.Msg e.Msgs
    , <Apply t.Update t.Msg t.Model> : t.NewModel
    = <UpdateForEachMsg t.Update t.NewModel e.Msgs>;
}


Render  {
  e.DOM
    , <Prout 'Render: ' e.DOM> :
    , <CNFGGetDimensions> : s.W s.H
    = <CNFGClearFrame>
      <Render-Elements e.DOM>
      <CNFGSwapBuffers>
}


Render-Elements {
  /* empty */ = /* empty */;

  t.Element e.Elements = <Render-Element t.Element> <Render-Elements e.Elements>;
}


Render-Element {
  (Text ((e.Color) (s.X s.Y) s.Scale) e.Text)
    = <CNFGColor e.Color>
      <CNFGSetPenX s.X>
      <CNFGSetPenY s.Y>
      <CNFGDrawText (e.Text) s.Scale>;
}


/* UI Kit */

$ENTRY Text {
  ((e.Color) (s.X s.Y) s.Scale) e.Text = (Text ((e.Color) (s.X s.Y) s.Scale) e.Text);
}
