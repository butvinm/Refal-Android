*$FROM LibraryEx
$EXTERN Apply, OneOf;

*$FROM refalrawdraw
$EXTERN
  CNFGSetupFullscreen,
  CNFGHandleInput,
  CNFGClearFrame,
  CNFGGetDimensions,
  CNFGSwapBuffers,
  CNFGDrawText,
  CNFGSetBGColor,
  CNFGColor,
  CNFGSetPenX,
  CNFGSetPenY,
  CNFGTackRectangle,
  RawDrawButtonEvents;


$ENTRY RunApplication {
  (e.Title) (e.BgColor) t.InitModel t.Update t.View
    , <Setup (e.Title) (e.BgColor)> : t.Ctx
    , <Apply t.InitModel> : t.Model
    = <MainLoop t.Ctx t.Model t.Update t.View>
}


Setup {
  (e.Title) (e.BgColor)
    , <CNFGSetupFullscreen (e.Title) 0>
      <CNFGSetBGColor e.BgColor>
    : e.e1
    = ((/* e.ButtonEventListeners */) /* reserved */)
}


ResetContext {
  ((e.ButtonEventListeners) e.Rest) = ((/* empty */) e.Rest);
}


MainLoop {
  t.Ctx t.Model t.Update t.View
    , <CNFGHandleInput>
    : {
        0 = /* exit */;

        1
          , <HandleEvents t.Ctx>
          : {
              /* empty */
                , <Apply t.View t.Model> : e.DOM
                , <Render <ResetContext t.Ctx> e.DOM> : t.NewCtx
                = <MainLoop t.NewCtx t.Model t.Update t.View>;

              e.Msgs
                , <UpdateForEachMsg t.Update t.Model e.Msgs> : t.NewModel
                , <Apply t.View t.Model> : e.DOM
                , <Render <ResetContext t.Ctx> e.DOM> : t.NewCtx
                = <MainLoop t.NewCtx t.NewModel t.Update t.View>;
            }
      }
}


HandleEvents {
  t.Ctx = <ListenButtonEvents t.Ctx <RawDrawButtonEvents>>
}


ListenButtonEvents {
  t.Ctx /* empty */ = /* empty */;

  t.Ctx t.Event e.Events
    , t.Ctx : ((e.ButtonEventListeners) e.Rest)
    = <ApplyAll t.Event e.ButtonEventListeners> <ListenButtonEvents t.Ctx e.Events>;
}


ApplyAll {
  t.Data /* empty */ = /* empty */;

  t.Data t.Fn e.Fns = <Apply t.Fn t.Data> <ApplyAll t.Data e.Fns>;
}


UpdateForEachMsg {
  t.Update t.Model /* empty */ = t.Model;

  t.Update t.Model t.Msg e.Msgs
    , <Apply t.Update t.Msg t.Model> : t.NewModel
    = <UpdateForEachMsg t.Update t.NewModel e.Msgs>;
}


Render  {
  t.Ctx e.DOM
    , <CNFGGetDimensions> : s.W s.H
    = <CNFGClearFrame>
      <Render-Elements t.Ctx e.DOM>
      <CNFGSwapBuffers>
}


Render-Elements {
   t.Ctx /* empty */ = t.Ctx;

   t.Ctx t.Element e.Elements
    , <Render-Element t.Ctx t.Element> : t.NewCtx
    = <Render-Elements t.NewCtx e.Elements>;
}


Render-Element {
  t.Ctx
  (Text ((e.Color) (s.X s.Y) s.Scale) e.Text)
    , <CNFGColor e.Color>
      <CNFGSetPenX s.X>
      <CNFGSetPenY s.Y>
      <CNFGDrawText (e.Text) s.Scale>
    : e.e1
    = t.Ctx;

  t.Ctx
  (Button ((e.Color) (s.X s.Y) (s.W s.H) t.OnReleaseMsg t.OnPressMsg))
    , <CNFGColor e.Color>
      <CNFGTackRectangle s.X s.Y s.W s.H>
    : e.e1
    , t.Ctx : ((e.ButtonEventListeners) e.Rest)
    = ((e.ButtonEventListeners (ButtonEventListener ((s.X s.Y) (s.W s.H) t.OnReleaseMsg t.OnPressMsg))) e.Rest);
}


$ENTRY ButtonEventListener {
  ((s.X1 s.Y1) (s.W s.H) t.OnReleaseMsg t.OnPressMsg)
  (s.EX s.EY s.Button s.bDown)
    , <Add s.X1 s.W> : s.X2
    , <Add s.Y1 s.H> : s.Y2
    , <OneOf <Compare s.EX s.X1> '+0'>  : True
    , <OneOf <Compare s.EX s.X2> '-0'>  : True
    , <OneOf <Compare s.EY s.Y1> '+0'>  : True
    , <OneOf <Compare s.EY s.Y2> '-0'>  : True
    , s.bDown
    : {
        0 = t.OnPressMsg;
        1 = t.OnReleaseMsg;
      };

  ((s.X1 s.Y1) (s.W s.H) t.OnReleaseMsg t.OnPressMsg)
  (s.EX s.EY s.Button s.bDown)
    = /* empty */;
}


/* UI Kit */

$ENTRY Text {
  ((e.Color) (s.X s.Y) s.Scale) e.Text = (Text ((e.Color) (s.X s.Y) s.Scale) e.Text);
}


$ENTRY Button {
  ((e.Color) (s.X s.Y) (s.W s.H) t.OnReleaseMsg t.OnPressMsg) = (Button ((e.Color) (s.X s.Y) (s.W s.H) t.OnReleaseMsg t.OnPressMsg));
}
